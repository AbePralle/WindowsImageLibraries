class WindowsMediaLibrariesPackage : Package
  # Base class Package is defined here:
  # https://github.com/AbePralle/Morlock/blob/main/Source/Package.rogue
  PROPERTIES
    name = "abepralle/windowsmedialibs"

  METHODS
    method install
      prior.install

      local zipfile = download_asset( "Libraries-$.zip"(version) )
      Zip( zipfile ).extract( install_folder )

      local versions = JSON.load( File(archive_folder)/"Versions.json" )
      println
      println "Libraries installed:"
      println "  - jpeg $"(versions//jpeg)
      println "  - lpng $"(versions//png)
      println "  - zlib $"(versions//zlib)
      println
      println "Run 'windowsmedialibs [includes|libs] [jpeg|png|zlib]' to obtain compile-time info.".word_wrapped(Console.width.or_smaller(80))

    method on( action:String )
      localize args
      if (args.is_empty) args.[ add("jpeg"), add("png"), add("zlib") ]

      local action_handled = false

      local result = ""
      if (action == "cflags" or action == "includes")
        local inc = which{ System.is_windows:"/I" || "-I" }
        result .= appending( inc + File(install_folder)/"Libraries/include" )
        action_handled = true
      endIf

      if (action == "cflags" or action == "libs")
        if (args.contains("jpeg")) result .= appending( find_libs( File(install_folder)/"Libraries/lib", "libjpeg" ) )
        if (args.contains("png"))  result .= appending( find_libs( File(install_folder)/"Libraries/lib", "libpng" )  )
        if (args.contains("zlib")) result .= appending( find_libs( File(install_folder)/"Libraries/lib", "zlib" ) )
        action_handled = true
      endIf

      if (action_handled)
        if (result.count) println result
      else
        prior.on( action )
      endIf

    method find_libs( folder:File, prefix:String )->String
      local result = ""
      forEach (lib in folder.listing)
        if (lib.ends_with(".lib") and File(lib).filename.begins_with(prefix)) result .= appending(lib)
      endForEach
      return result

endClass
